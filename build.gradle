plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.1'
	id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.portal'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

sourceSets {
	test {
		java {
			srcDir 'src/test/java'
		}
		resources {
			srcDir 'src/test/resources'
		}
	}
	integrationTest {
		java {
			srcDir 'src/testIntegration/java'
		}
		resources {
			srcDir 'src/testIntegration/resources'
		}
		compileClasspath += sourceSets.main.output + configurations.testRuntimeClasspath
		runtimeClasspath += sourceSets.main.output + configurations.testRuntimeClasspath
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}

	integrationTestImplementation.extendsFrom testImplementation
	integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'com.mysql:mysql-connector-j'
	annotationProcessor 'org.projectlombok:lombok'

	// Spring Boot Test (Includes JUnit 5 and other test dependencies)
	testImplementation 'org.springframework.boot:spring-boot-starter-test'

	// JUnit Jupiter (JUnit 5)
	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.11.0' // Use the latest stable release
	testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.11.0'
	testImplementation 'org.junit.jupiter:junit-jupiter-params:5.11.0'

	// JUnit Platform Launcher (for integrating JUnit into the test infrastructure)
	//testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.9.2'

	// Mockito for mocking
	//testImplementation 'org.mockito:mockito-core:5.0.0'
	//testImplementation 'org.mockito:mockito-junit-jupiter:5.0.0'

	integrationTestImplementation 'org.springframework.boot:spring-boot-starter-test'
	integrationTestImplementation 'org.springframework.boot:spring-boot-testcontainers'
	integrationTestImplementation 'org.testcontainers:junit-jupiter'
	integrationTestImplementation 'org.testcontainers:mysql'
	integrationTestRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	useJUnitPlatform()
	testLogging {
		events "passed", "failed", "skipped"
		showStandardStreams = true
		exceptionFormat = "full"
	}
}

// Register the 'integrationTest' task to run integration tests
tasks.register('integrationTest', Test) {
	description = "Runs the integration tests."
	group = "verification"

	testClassesDirs = sourceSets.integrationTest.output.classesDirs
	classpath = sourceSets.integrationTest.runtimeClasspath

	useJUnitPlatform()

	testLogging {
		events "passed", "failed", "skipped"
		showStandardStreams = true
		exceptionFormat = "full"
	}

	// Specify the Spring profile for integration tests
	systemProperty 'spring.profiles.active', 'integration'
}

